/*
 * Copyright (C)2017 Wiggin77
 *
 * Permission is hereby granted, free of charge, to any person obtaining a
 * copy of this software and associated documentation files (the "Software"),
 * to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */
package wiggin.util;

import wiggin.util.StringUtil;

class RegExUtil
{
	/**
	*  Escapes the regex pattern string such that all regex special characters 
	*  become plain text for the purposes of pattern matching.
	* 
	*  @param strPattern - the pattern to escape.
	*  @return String
	*/
	public static function escapeRegExPattern(strPattern:String) : String
	{
		var r = ~/([\\*+\[\](){}\$.?\^|])/g;
		return r.replace(strPattern, #if java "\\\\$1" #else "\\$1" #end);
	}

	/**
	 *  Escapes all characters in `strChars` such that they can be safely used in
	 *  character ranges.
	 *  @param strChars - the character sequence to escape. Cannot be null.
	 *  @return String - the escaped sequence
	 */
	public static function escapeRegexChars(strChars:String) : String
	{
		if(!StringUtil.hasChars(strChars))
		{
			return "";
		}

		var sb = new StringBuf();
		var it = new StringIterator(strChars);
		var arrMeta = ["^", "[", "." , "$", "{", "*", "(", "\\", "/", "+", ")", "|", "?", "<", ">", " "];
		for(ch in it)
		{
			if(StringUtil.contains(ch, arrMeta))
			{
				sb.add("\\");
			}
			sb.add(ch);
		}
		return sb.toString();
	}

}